plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'net.flectone.chat.reborn'
version = '4.5.2'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name 'codemc'
        url 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        name = "loohp-repo"
        url = "https://repo.loohpjames.com/repository"
    }
    maven {
        name = "m2-dv8tion"
        url = "https://m2.dv8tion.net/releases"
    }
    maven {
        name = "Scarsz-Nexus"
        url = "https://nexus.scarsz.me/content/groups/public/"
    }

    maven {
        url = 'https://maven.maxhenkel.de/repository/public'
    }

    maven {
        name = "iridiumdevelopment"
        url = "https://nexus.iridiumdevelopment.net/repository/maven-releases/"
    }

    maven {
        url = "https://repo.plo.su"
    }

}

dependencies {
    implementation files("LICENSE")

    implementation "com.iridium:IridiumColorAPI:${project.iridiumcolorapi}"
    implementation 'org.jetbrains:annotations:24.0.0'

    compileOnly "su.plo.voice.api:server:${project.plasmovoice_api}"

    compileOnly "net.skinsrestorer:skinsrestorer-api:${project.skinsrestorer_api}"

    compileOnly "com.loohp:InteractiveChat:${project.interactivechat_version}"
    compileOnly "com.github.MilkBowl:VaultAPI:${project.vault_api}"
    compileOnly "me.clip:placeholderapi:${project.placeholder_api}"
    compileOnly "de.maxhenkel.voicechat:voicechat-api:${project.voicechat_api}"
    compileOnly "net.luckperms:api:${project.luckperms_api}"
    compileOnly "com.github.LeonMangler:SuperVanish:${project.super_vanish}"
    compileOnly "com.discordsrv:discordsrv:${project.discordsrv_version}"
    compileOnly "net.md-5:bungeecord-api:${project.bungeecord_api}"

    compileOnly "org.spigotmc:spigot-api:${project.spigot_version}"

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "FlectoneChatReborn-" + version + ".jar"
    dependencies {
        exclude(dependency('org.jetbrains:annotations:24.0.0'))
    }
}